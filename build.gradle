plugins {
    id 'java'
    id 'maven-publish'
}

group = 'com.dumptruckman.minecraft'
version = '2.0-SNAPSHOT'
description = 'A JSON Configuration for Bukkit based on FileConfiguration.'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    maven {
        name = 'spigot-repo'
        url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
    }
    maven {
        name = 'onarandombox'
        url = uri('https://repo.onarandombox.com/content/groups/public/')
    }
}

dependencies {
    // Spigot API
    compileOnly 'org.bukkit:bukkit:1.12-R0.1-SNAPSHOT'
    
    // JSON-smart
    implementation 'net.minidev:json-smart:2.5.1'
    
    // IntelliJ Annotations
    compileOnly 'com.intellij:annotations:12.0'

    // Testing libraries
    testImplementation 'org.bukkit:bukkit:1.12-R0.1-SNAPSHOT'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.5'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.5'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

publishing {
    repositories {
        maven {
            name = 'OnARandomBox'
            url = uri('https://repo.c0ding.party/releases/')
            credentials(PasswordCredentials)
        }
        maven {
            name = 'OnARandomBox-Snapshot'
            url = uri('https://repo.c0ding.party/snapshots/')
            credentials(PasswordCredentials)
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

test {
    useJUnit()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
